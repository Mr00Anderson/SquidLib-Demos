plugins { id 'org.beryx.runtime' version '1.8.4' }
apply plugin: 'application'

mainClassName = 'com.squidpony.demo.lwjgl3.Lwjgl3Launcher'
def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)

eclipse.project.name = appName + '-lwjgl3'
sourceCompatibility = 1.8
sourceSets.main.resources.srcDirs += [ file('../assets').absolutePath ]

dependencies {
  compile project(':core')
  compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
  compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

jar {
  destinationDirectory = file("$project.buildDir/lib")
  archiveName "${appName}.jar"
  from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
  manifest {
    attributes 'Main-Class': project.mainClassName
  }
}
jpackageImage.dependsOn jar

runtime {
  options.set(['--strip-debug',
             '--compress', '2',
             '--no-header-files',
             '--no-man-pages',
             '--strip-native-commands',
             '--vm', 'server'])
  modules.set(['java.base' ,
             'java.desktop',
             'jdk.unsupported'])
  distDir.set(file(buildDir))
  jpackage {
    imageName = 'NorthernLights'
    skipInstaller = true
    jpackageHome = javaHome.getOrElse("")
    mainJar = jar.archiveFileName.get()
    if (osName.contains('windows')) {
      imageOptions = ["--icon", "../icons/icon.ico"]
    } else if (osName.contains('linux')) {
      imageOptions = ["--icon", "../icons/icon.png"]
    } else if (osName.contains('mac')) {
      imageOptions = ["--icon", "../icons/icon.icns"]
    }
  }
}
